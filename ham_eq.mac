ham_eq(x,y):=block(

xdot:diff(x,t),
ydot:diff(y,t),

T:factor(trigsimp(trigexpand(1/2*m*(xdot.xdot+ydot.ydot))))/m/l^2,
V:ratsimp(sum (m*g*y[i], i, 1, 2)/m/g/l),

LaG:trigreduce(trigexpand(T-V)),
disp("The Lagrangian of the system is:", L = LaG),

pq:factor(trigsimp(trigexpand([diff(LaG,qdot[1]),diff(LaG,qdot[2])]))),

sol:solve([p[1]=pq[1],p[2]=pq[2]],[qdot[1],qdot[2]]),
qdotsol:[0,0], for i: 1 thru length(qdotsol) do qdotsol[i]:rhs(sol[1][i]),

LaG:subst([qdot[1] = qdotsol[1], qdot[2] =qdotsol[2]], LaG),

Ham:sum (p[i]*qdotsol[i], i, 1, 2) - LaG,

Xdot:[trigsimp(diff(Ham,p[1])),trigsimp(diff(Ham,p[2])),-diff(Ham,q[1]),trigsimp(-diff(Ham,q[2]))],
X:[q[1],q[2],p[1],p[2]],

disp("The generalized momenta are:"),
disp(p[1] = pq[1]),
disp(p[2] = pq[2]),
disp("The generalized coordinates rate of change is:"),
disp(qdot[1] = qdotsol[1]),
disp(qdot[2] = qdotsol[2]),
disp("The Hamiltonian of the system is:", H = trigsimp(Ham)),
disp("Hamilton's equations of motion are:"),
disp(qdot[1] = Xdot[1]),
disp(qdot[2] = Xdot[2]),
disp('diff(p[1],t) = Xdot[3]),
disp('diff(p[2],t) = Xdot[4])
)$